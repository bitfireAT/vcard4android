
buildscript {
    ext.versions = [
        kotlin: '1.4.10',
        dokka: '0.10.1',
        // latest Apache Commons versions that don't require Java 8 (Android 7)
        commonsIO: '2.6',
        commonsText: '1.3'
    ]

    repositories {
        jcenter()
        google()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:4.0.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:${versions.dokka}"
    }
}

repositories {
    jcenter()
    google()
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'org.jetbrains.dokka'

android {
    compileSdkVersion 29
    buildToolsVersion '30.0.2'

    defaultConfig {
        minSdkVersion 16        // Android 4.1
        targetSdkVersion 29     // Android 10
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    lintOptions {
        disable 'AllowBackup'
        disable 'InvalidPackage'
    }
    packagingOptions {
        exclude 'LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }

    defaultConfig {
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    dokka.configuration {
        sourceLink {
            url = "https://gitlab.com/bitfireAT/vcard4android/tree/master/"
            lineSuffix = "#L"
        }
        jdkVersion = 7
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:${versions.kotlin}"

    implementation 'androidx.annotation:annotation:1.1.0'
    // noinspection GradleDependency
    implementation "commons-io:commons-io:${versions.commonsIO}"
    // noinspection GradleDependency
    implementation "org.apache.commons:commons-text:${versions.commonsText}"

    // ez-vcard to parse/generate VCards
    api('com.googlecode.ez-vcard:ez-vcard:0.11.0') {
        // hCard functionality not needed
        exclude group: 'org.jsoup'
        exclude group: 'org.freemarker'
        // jCard functionality not needed
        exclude group: 'com.fasterxml.jackson.core'
    }

    androidTestImplementation 'androidx.test:runner:1.3.0'
    androidTestImplementation 'androidx.test:rules:1.3.0'

    testImplementation 'junit:junit:4.13'
}
