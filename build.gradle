buildscript {
    ext.versions = [
        kotlin: '1.8.20',
        dokka: '1.8.10',
        // latest Apache Commons versions that don't require Java 8 (Android 7)
        commonsIO: '2.6',
        commonsText: '1.3'
    ]

    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:8.0.0'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:${versions.dokka}"
    }
}

repositories {
    google()
    mavenCentral()
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'org.jetbrains.dokka'

android {
    compileSdkVersion 33
    buildToolsVersion '33.0.2'

    defaultConfig {
        minSdkVersion 21        // Android 5
        targetSdkVersion 33     // Android 13
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17

        coreLibraryDesugaringEnabled true
    }

    buildFeatures {
        buildConfig = true
    }

    namespace 'at.bitfire.vcard4android'

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    packagingOptions {
        resources {
            excludes += ['LICENSE', 'META-INF/LICENSE.txt', 'META-INF/NOTICE.txt']
        }
    }

    lint {
        disable 'AllowBackup', 'InvalidPackage'
    }

    defaultConfig {
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:${versions.kotlin}"
    coreLibraryDesugaring("com.android.tools:desugar_jdk_libs:2.0.3")

    implementation 'androidx.annotation:annotation:1.6.0'
    // noinspection GradleDependency
    implementation "commons-io:commons-io:${versions.commonsIO}"
    // noinspection GradleDependency
    implementation "org.apache.commons:commons-text:${versions.commonsText}"

    // ez-vcard to parse/generate vCards
    api('com.googlecode.ez-vcard:ez-vcard:0.12.0') {    // requires Java 8
        // hCard functionality not needed
        exclude group: 'org.jsoup'
        exclude group: 'org.freemarker'
    }

    androidTestImplementation 'androidx.test:runner:1.5.2'
    androidTestImplementation 'androidx.test:rules:1.5.0'

    testImplementation 'junit:junit:4.13.2'
}